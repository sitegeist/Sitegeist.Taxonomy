prototype(Sitegeist.Taxonomy:Component.FlashMessages) < prototype(Neos.Fusion:Component) {
    flashMessages = ${flashMessages || []}

    renderer = afx`
        <div id="neos-notification-container" class="neos-notification-top" @if.hasMessages={props.flashMessages}>
            <Neos.Fusion:Loop items={props.flashMessages} itemName="message">
                <Sitegeist.Taxonomy:Component.FlashMessages.Message message={message}/>
            </Neos.Fusion:Loop>
        </div>
    `
}

prototype(Sitegeist.Taxonomy:Component.FlashMessages.Message) < prototype(Neos.Fusion:Component) {
    message = ${{}}

    severity = ${String.toLowerCase(this.message.severity)}
    severity.@process.replaceOKStatus = ${value == 'ok' ? 'success' : value}
    severity.@process.replaceNoticeStatus = ${value == 'notice' ? 'info' : value}

    renderer = afx`
        <div class={'neos-notification neos-notification-' + props.severity}>
            <div class={'neos-notification-content' + (props.message.message ? ' expandable' : '')}>
                <i class={'fas fa-' + props.severity}></i>
                <div class="neos-notification-heading" @if.hasTitle={props.message.title}>
                    {props.message.title}
                </div>
                <div class="neos-expand-content">{props.message.message}</div>
            </div>
        </div>
    `
}
