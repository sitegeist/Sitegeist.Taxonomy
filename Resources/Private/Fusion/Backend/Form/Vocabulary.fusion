prototype(Sitegeist.Taxonomy:Form.Vocabulary) < prototype(Neos.Fusion:Component) {

    i18nMain = ${Translation.value('').package("Sitegeist.Taxonomy").source('Main')}
    i18nVocabulary = ${Translation.value('').package("Sitegeist.Taxonomy").source('NodeTypes/Vocabulary')}

    targetAction = null
    rootNode = null
    vocabularyNode = null


    additionalFieldPrototypeNames = ${Configuration.setting('Sitegeist.Taxonomy.backendModule.additionalVocabularyFieldPrototypes')}

    renderer = afx`
        <Neos.Fusion.Form:Form
          form.target.action={props.targetAction}
          form.data.name={props.vocabularyNode.nodeName.value}
          form.data.properties={props.vocabularyNode.properties}
        >
            <Neos.Fusion.Form:Hidden field.name="vocabularyNodeAddress" field.value={props.vocabularyNode ? Neos.Node.serializedNodeAddress(props.vocabularyNode) : null} @if.has={props.vocabularyNode}/>
            <Neos.Fusion.Form:Hidden field.name="rootNodeAddress" field.value={props.rootNode ? Neos.Node.serializedNodeAddress(props.rootNode) : null} @if.has={props.rootNode}/>

            <fieldset>
                <div class="neos-control-group">
                    <label class="neos-control-label" for="name">
                        {props.i18nVocabulary.id('properties.name')}
                    </label>
                    <Neos.Fusion.Form:Input
                        attributes.class="neos-span6"
                        field.name="name"
                        attributes.required attributes.pattern="[a-z0-9]+"
                        attributes.readonly="1" attributes.readonly.@if={props.vocabularyNode.classification.value == 'tethered'}

                    />
                </div>

                <div class="neos-control-group">
                    <label class="neos-control-label" for="title">
                        {props.i18nVocabulary.id('properties.title')}
                    </label>
                    <Neos.Fusion.Form:Input attributes.class="neos-span6" field.name="properties[title]" attributes.required />
                </div>

                <div class="neos-control-group">
                    <label class="neos-control-label" for="description">
                        {props.i18nVocabulary.id('properties.description')}
                    </label>
                    <Neos.Fusion.Form:Textarea attributes.class="neos-span6 form-inline" field.name="properties[description]"/>
                </div>

                <Neos.Fusion:Fragment @if={props.additionalFieldPrototypeNames}>
                    <Neos.Fusion:Loop items={props.additionalFieldPrototypeNames} itemKey="key" itemName="prototypeName">
                        <div class="neos-control-group">
                            <Neos.Fusion:Renderer
                                type={prototypeName}
                                element.name={key}
                                element.rootNode={props.rootNode}
                                element.vocabularyNode={props.vocabularyNode}
                            />
                        </div>
                    </Neos.Fusion:Loop>
                </Neos.Fusion:Fragment>

                <div class="neos-control-group">
                    <Neos.Fusion:Link.Action
                        class="neos-button"
                        href.action="index"
                        href.arguments.rootNodeAddress={Neos.Node.serializedNodeAddress(props.rootNode)}
                    >
                        {props.i18nMain.id('generic.cancel')}
                    </Neos.Fusion:Link.Action>
                    &nbsp;
                    <Neos.Fusion.Form:Button attributes.class="neos-button neos-button-primary">{props.i18nMain.id('generic.save') + ''}</Neos.Fusion.Form:Button>
                </div>
            </fieldset>
        </Neos.Fusion.Form:Form>
    `

}
