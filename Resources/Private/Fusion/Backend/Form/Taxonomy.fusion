prototype(Sitegeist.Taxonomy:Form.Taxonomy) < prototype(Neos.Fusion:Component) {

    i18nMain = ${Translation.value('').package("Sitegeist.Taxonomy").source('Main')}
    i18nVocabulary = ${Translation.value('').package("Sitegeist.Taxonomy").source('NodeTypes/Vocabulary')}
    i18nTaxonomy = ${Translation.value('').package("Sitegeist.Taxonomy").source('NodeTypes/Taxonomy')}

    targetAction = null
    taxonomyNode = null
    parentNode = null

    additionalFieldPrototypeNames = ${Configuration.setting('Sitegeist.Taxonomy.backendModule.additionalTaxonomyFieldPrototypes')}

    renderer = afx`
        <Neos.Fusion.Form:Form
          form.target.action={props.targetAction}
          form.data.name={props.taxonomyNode.nodeName.value}
          form.data.properties={props.taxonomyNode.properties}
        >

            <Neos.Fusion.Form:Hidden field.name="taxonomyNodeAddress" field.value={props.taxonomyNode ? Neos.Node.serializedNodeAddress(props.taxonomyNode) : null} @if.has={props.taxonomyNode}/>
            <Neos.Fusion.Form:Hidden field.name="parentNodeAddress" field.value={props.parentNode ? Neos.Node.serializedNodeAddress(props.parentNode) : null} @if.has={props.parentNode}/>

            <fieldset>
                <div class="neos-control-group">
                    <label class="neos-control-label" for="title">
                        {props.i18nTaxonomy.id('properties.name')}
                        <span @if={props.defaultTaxonomy}>: {props.defaultTaxonomy.properties.name}</span>
                    </label>
                    <Neos.Fusion.Form:Input
                        attributes.class="neos-span6"
                        field.name="name"
                        attributes.required attributes.pattern="[a-z0-9]+"
                        attributes.disabled="1" attributes.disabled.@if={props.taxonomyNode.classification.value == 'tethered'}
                    />
                </div>

                <div class="neos-control-group">
                    <label class="neos-control-label" for="title">
                        {props.i18nTaxonomy.id('properties.title')}
                        <span @if={props.defaultTaxonomy}>: {props.defaultTaxonomy.properties.title}</span>
                    </label>
                    <Neos.Fusion.Form:Input attributes.class="neos-span6" field.name="properties[title]" attributes.required />
                </div>

                <div class="neos-control-group">
                    <label class="neos-control-label" for="description">
                        {props.i18nTaxonomy.id('properties.description')}
                        <span @if={props.defaultTaxonomy}>: {props.defaultTaxonomy.properties.description}</span>
                    </label>
                    <Neos.Fusion.Form:Textarea attributes.class="neos-span6 form-inline" field.name="properties[description]"/>
                </div>

                <Neos.Fusion:Fragment @if={props.additionalFieldPrototypeNames}>
                    <Neos.Fusion:Loop items={props.additionalFieldPrototypeNames} itemKey="key" itemName="prototypeName">
                        <div class="neos-control-group">
                            <Neos.Fusion:Renderer
                                type={prototypeName}

                                element.name={key}
                                element.taxonomy={props.taxonomy}
                                element.defaultTaxonomy={props.defaultTaxonomy}
                            />
                        </div>
                    </Neos.Fusion:Loop>
                </Neos.Fusion:Fragment>

                <div class="neos-control-group">
                    <Neos.Fusion:Link.Action
                        class="neos-button"
                        href.action="vocabulary"
                        href.arguments.vocabulary={props.vocabulary}
                    >
                        {props.i18nMain.id('generic.cancel')}
                    </Neos.Fusion:Link.Action>
                    &nbsp;
                    <Neos.Fusion.Form:Button attributes.class="neos-button neos-button-primary">{props.i18nMain.id('generic.save') + ''}</Neos.Fusion.Form:Button>
                </div>
            </fieldset>
        </Neos.Fusion.Form:Form>
    `

}
