prototype(Sitegeist.Taxonomy:Views.Module.Taxonomy.List) < prototype(Neos.Fusion:Component) {

    i18n = ${Translation.value('').package("Sitegeist.Taxonomy").source('Main')}
    i18nVocabulary = ${Translation.value('').package("Sitegeist.Taxonomy").source('NodeTypes/Vocabulary')}
    i18nTaxonomy = ${Translation.value('').package("Sitegeist.Taxonomy").source('NodeTypes/Taxonomy')}

    renderer = afx`
        <div class="neos-row-fluid">
            <legend>
                {props.i18n.id('vocabulary')} "{vocabularyNode.properties.title}" [{vocabularyNode.nodeName.value}]
                <div class="neos-pull-right">
                    <Sitegeist.Taxonomy:Views.Fragments.DimensionSelector
                        targetAction='vocabulary'
                        targetProperty='vocabularyNodeAddress'
                        contextNode={vocabularyNode}
                    />
                </div>
            </legend>
        </div>

        <p>
            {vocabularyNode.properties.description}
        </p>

        <p @if={!vocabularySubtree.children}>
            {props.i18n.id('vocabulary.empty')}
        </p>

        <table class="neos-table" @if={vocabularySubtree.children}>
            <thead>
            <tr>
                <th>
                    {props.i18nTaxonomy.id('properties.title')}
                </th>
                <th>
                    {props.i18nTaxonomy.id('properties.description')}
                </th>
            </tr>
            </thead>
            <tbody>
            <Neos.Fusion:Loop items={vocabularySubtree.children} itemName="taxonomySubtree">
                <Sitegeist.Taxonomy:Views.Module.Taxonomy.List.Item taxonomySubtree={taxonomySubtree}/>
            </Neos.Fusion:Loop>
            </tbody>
        </table>

        <div class="neos-row-fluid" @if={Security.hasAccess("Sitegeist.Taxonomy:Module.ManageTaxonomyActions")}>
            <Neos.Fusion:Link.Action
                href.action="index"
                href.arguments.rootNodeAddress={rootNode}
                class="neos-button"
                title=""
            >
                {props.i18n.id('generic.back')}
            </Neos.Fusion:Link.Action>
            &nbsp;
            <Neos.Fusion:Link.Action
                href.action="newTaxonomy"
                href.arguments.parentNodeAddress={Neos.Node.serializedNodeAddress(vocabularyNode)}
                class="neos-button neos-button-primary"
                title=""
            >
                {props.i18n.id('taxon.create')}
            </Neos.Fusion:Link.Action>
        </div>
    `
}


prototype(Sitegeist.Taxonomy:Views.Module.Taxonomy.List.Item) < prototype(Neos.Fusion:Component) {

    taxonomySubtree = null

    renderer = afx`
        <tr>
            <td>
                <Neos.Fusion:Loop items={Array.range(0, props.taxonomySubtree.level - 2)} @if={props.taxonomySubtree.level > 1}>
                    &nbsp; &nbsp; &nbsp;
                </Neos.Fusion:Loop>
                <i class={props.taxonomySubtree.node.nodeType.fullConfiguration.ui.icon}></i>
                &nbsp; &nbsp;
                {props.taxonomySubtree.node.properties.title} [{props.taxonomySubtree.node.nodeName.value}]
            </td>
            <td>
                {props.taxonomySubtree.node.properties.description}
            </td>
            <td>
                <div class="neos-pull-right">

                    <Neos.Fusion:Fragment @if={Security.hasAccess("Sitegeist.Taxonomy:Module.ManageTaxonomyActions")}>

                        <Neos.Fusion:Link.Action
                            href.action="newTaxonomy"
                            href.arguments.parentNodeAddress={Neos.Node.serializedNodeAddress(props.taxonomySubtree.node)}
                            title={props.i8n.id('taxon.createBelow')}
                            class="neos-button neos-button-primary"
                        >
                            <i class="fas fa-plus"></i>
                        </Neos.Fusion:Link.Action>
                        &nbsp;
                        <Neos.Fusion:Link.Action
                            href.action="editTaxonomy"
                            href.arguments.taxonomyNodeAddress={Neos.Node.serializedNodeAddress(props.taxonomySubtree.node)}
                            title={props.i8n.id('generic.edit')}
                            class="neos-button neos-button-primary"
                        >
                            <i class="fas fa-pencil-alt"></i>
                        </Neos.Fusion:Link.Action>

                        <Neos.Fusion:Fragment @if={!props.taxonomySubtree.autoCreated}>
                            &nbsp;
                            <button class="neos-button neos-button-danger"
                                title={props.i8n.id('generic.delete')}
                                data-toggle="modal"
                                href={"#deleteTaxonomy_" + props.taxonomySubtree.node.nodeAggregateId.value}
                                data-neos-toggle="tooltip"
                            >
                                <i class="fas fa-trash-alt icon-white"></i>
                            </button>
                            <div class="neos-hide" id={"deleteTaxonomy_" + props.taxonomySubtree.node.nodeAggregateId.value}>
                                <div class="neos-modal-centered">
                                    <div class="neos-modal-content">
                                        <div class="neos-modal-header">
                                            <button type="button" class="neos-close neos-button" data-dismiss="modal"></button>
                                            <div class="neos-header">Do you really want to delete the taxonomySubtreeomy "{ props.taxonomySubtree.node.properties.title}"? This action cannot be undone.</div>
                                        </div>
                                        <div class="neos-modal-footer">
                                            <a href="#" class="neos-button" data-dismiss="modal">Cancel</a>
                                            <Neos.Fusion.Form:Form form.target.action="deleteTaxonomy" form.target.arguments.taxonomyNodeAddress={Neos.Node.serializedNodeAddress(props.taxonomySubtree.node)} attributes.class="neos-inline">
                                                <button class="neos-button neos-button-danger" title="Delete this taxonomy">Yes, delete this taxonomy</button>
                                            </Neos.Fusion.Form:Form>
                                        </div>
                                    </div>
                                </div>
                                <div class="neos-modal-backdrop neos-in"></div>
                            </div>

                        </Neos.Fusion:Fragment>

                    </Neos.Fusion:Fragment>

                </div>
            </td>
        </tr>

        <Neos.Fusion:Loop items={props.taxonomySubtree.children} itemName="child">
            <Sitegeist.Taxonomy:Views.Module.Taxonomy.List.Item taxonomySubtree={child}/>
        </Neos.Fusion:Loop>

    `
}
