prototype(Sitegeist.Taxonomy:Views.Module.Taxonomy.List) < prototype(Neos.Fusion:Component) {

    i18n = ${Translation.value('').package("Sitegeist.Taxonomy").source('Main')}
    i18nVocabulary = ${Translation.value('').package("Sitegeist.Taxonomy").source('NodeTypes/Vocabulary')}
    i18nTaxonomy = ${Translation.value('').package("Sitegeist.Taxonomy").source('NodeTypes/Taxonomy')}

    renderer = afx`
        <div class="neos-row-fluid">
            <legend>
                {props.i18n.id('vocabulary')} {vocabulary.properties.title}
                <Neos.Fusion:Fragment @if={defaultVocabulary}>({defaultVocabulary.properties.title})</Neos.Fusion:Fragment>
                <div class="neos-pull-right">
                    <Sitegeist.Taxonomy:Views.Fragments.LanguageSelector
                        targetAction='vocabulary'
                        targetProperty='vocabulary'
                        contentDimensionOptions={contentDimensionOptions}
                        contextNode={vocabulary}
                    />
                </div>
            </legend>
        </div>

        <p>
            {vocabulary.properties.description}
        </p>

        <p @if={!vocabulary.childNodes}>
            {props.i18n.id('vocabulary.empty')}
        </p>

        <table class="neos-table" @if={vocabulary.childNodes}>
            <thead>
            <tr>
                <th>
                    {props.i18nTaxonomy.id('properties.title')}
                </th>
                <th>
                    {defaultVocabulary ? 'Default' : ''}
                </th>
                <th></th>
            </tr>
            </thead>
            <tbody>
            <Neos.Fusion:Loop items={taxonomies} itemName="taxon">
                <Sitegeist.Taxonomy:Views.Module.Taxonomy.List.Item taxon={taxon} isFirst={iterator.isFirst} isLast={iterator.isLast}/>
            </Neos.Fusion:Loop>
            </tbody>
        </table>

        <div class="neos-row-fluid" @if={Security.hasAccess("Sitegeist.Taxonomy:Module.ManageTaxonomyActions")}>
            <Neos.Fusion:Link.Action
                href.action="index"
                href.arguments.root={taxonomyRoot}
                class="neos-button"
                title=""
            >
                {props.i18n.id('generic.back')}
            </Neos.Fusion:Link.Action>
            &nbsp;
            <Neos.Fusion:Link.Action
                href.action="newTaxonomy"
                href.arguments.parent={vocabulary.contextPath}
                class="neos-button neos-button-primary"
                title=""
            >
                {props.i18n.id('taxon.create')}
            </Neos.Fusion:Link.Action>
        </div>
    `
}


prototype(Sitegeist.Taxonomy:Views.Module.Taxonomy.List.Item) < prototype(Neos.Fusion:Component) {
    taxon = null
    isFirst = false
    isLast = false

    renderer = afx`
        <tr>
            <td>
                <Neos.Fusion:Loop items={props.taxon.parents}>
                    &nbsp; &nbsp; &nbsp;
                </Neos.Fusion:Loop>
                <i class={props.taxon.parents ? 'fas fa-caret-right': props.taxon.node.nodeType.fullConfiguration.ui.icon} ></i>
                &nbsp;
                {props.taxon.node.properties.title}
            </td>
            <td>
               {props.taxon.defaultNode ? props.taxon.defaultNode.properties.title : ''}
            </td>
            <td>
                {props.taxon.node.properties.description}
            </td>
            <td>
                <div class="neos-pull-right">

                    <Neos.Fusion:Fragment @if={Security.hasAccess("Sitegeist.Taxonomy:Module.ManageTaxonomyActions")}>

                        <Neos.Fusion:Link.Action
                            @if={!props.isFirst}
                            href.action="moveUpTaxonomy"
                            href.arguments.taxonomy={props.taxon.node.contextPath}
                            title={props.i8n.id('taxon.moveUp')}
                            class="neos-button neos-button-primary"
                        >
                            <i class="fas fa-arrow-up"></i>
                        </Neos.Fusion:Link.Action>
                        <span @if={props.isFirst} class="neos-button neos-disabled" style="width: 46px;"></span>
                        <Neos.Fusion:Link.Action
                            @if={!props.isLast}
                            href.action="moveDownTaxonomy"
                            href.arguments.taxonomy={props.taxon.node.contextPath}
                            title={props.i8n.id('taxon.moveDown')}
                            class="neos-button neos-button-primary"
                        >
                            <i class="fas fa-arrow-down"></i>
                        </Neos.Fusion:Link.Action>
                        <span @if={props.isLast} class="neos-button neos-disabled" style="width: 46px;"></span>
                        <Neos.Fusion:Link.Action
                            href.action="newTaxonomy"
                            href.arguments.parent={props.taxon.node.contextPath}
                            title={props.i8n.id('taxon.createBelow')}
                            class="neos-button neos-button-primary"
                        >
                            <i class="fas fa-plus"></i>
                        </Neos.Fusion:Link.Action>
                        &nbsp;
                        <Neos.Fusion:Link.Action
                            href.action="editTaxonomy"
                            href.arguments.taxonomy={props.taxon.node.contextPath}
                            title={props.i8n.id('generic.edit')}
                            class="neos-button neos-button-primary"
                        >
                            <i class="fas fa-pencil-alt"></i>
                        </Neos.Fusion:Link.Action>

                        <Neos.Fusion:Fragment @if={!props.taxon.autoCreated}>
                            &nbsp;
                            <button class="neos-button neos-button-danger"
                                title={props.i8n.id('generic.delete')}
                                data-toggle="modal"
                                href={"#deleteTaxonomy_" + props.taxon.node.identifier}
                                data-neos-toggle="tooltip"
                            >
                                <i class="fas fa-trash-alt icon-white"></i>
                            </button>
                            <div class="neos-hide" id={"deleteTaxonomy_" + props.taxon.node.identifier}>
                                <div class="neos-modal-centered">
                                    <div class="neos-modal-content">
                                        <div class="neos-modal-header">
                                            <button type="button" class="neos-close neos-button" data-dismiss="modal"></button>
                                            <div class="neos-header">Do you really want to delete the taxonomy "{taxon.node.properties.title}"? This action cannot be undone.</div>
                                        </div>
                                        <div class="neos-modal-footer">
                                            <a href="#" class="neos-button" data-dismiss="modal">Cancel</a>
                                            <Neos.Fusion.Form:Form form.target.action="deleteTaxonomy" form.target.arguments.taxonomy={props.taxon.node} attributes.class="neos-inline">
                                                <button class="neos-button neos-button-danger" title="Delete this taxonomy">Yes, delete this taxonomy</button>
                                            </Neos.Fusion.Form:Form>
                                        </div>
                                    </div>
                                </div>
                                <div class="neos-modal-backdrop neos-in"></div>
                            </div>

                        </Neos.Fusion:Fragment>

                    </Neos.Fusion:Fragment>

                </div>
            </td>
        </tr>

        <Neos.Fusion:Loop items={props.taxon.children} itemName="child">
            <Sitegeist.Taxonomy:Views.Module.Taxonomy.List.Item taxon={child} isFirst={iterator.isFirst} isLast={iterator.isLast}/>
        </Neos.Fusion:Loop>

    `
}
